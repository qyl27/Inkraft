plugins {
    id 'com.github.johnrengelman.shadow' version "${shadow_plugin_version}"
    id 'me.shedaniel.unified-publishing' version "${unified_publishing_version}"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(':').loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig 'inkraft-common.mixins.json'
        mixinConfig 'inkraft-forge.mixins.json'
    }

    mods {
        'inkraft' {
            sourceSet project(':').sourceSets.main
            sourceSet sourceSets.main
        }

        'inkraft_test' {
            sourceSet project(':').sourceSets.test
        }
    }

    runs {
        client {
            client()

            runDir 'run/client'
        }

        server {
            server()

            runDir 'run/server'
        }

        data {
            data()

            runDir 'run/data'

            programArgs "--all", "--mod", "inkraft"
            programArgs "--output", file('../src/generated/resources').absolutePath
            programArgs "--existing", file('../src/main/resources').absolutePath
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
    modApi "dev.architectury:architectury-forge:${project.architectury_version}"

    common(project(path: ':', configuration: 'namedElements')) { transitive = false }
    shadowCommon(project(path: ':', configuration: 'transformProductionForge')) { transitive = false }

    forgeRuntimeLibrary "com.bladecoder.ink:blade-ink:${project.blade_ink_version}"
    shadowCommon "com.bladecoder.ink:blade-ink:${project.blade_ink_version}"
}

shadowJar {
    exclude 'fabric.mod.json'
    exclude 'architectury.common.json'

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = 'dev-shadow'

    relocate 'com.bladecoder.ink', 'cx.rain.mc.inkraft.libs.com.bladecoder.ink'
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(':').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = project.name
            version = 'true'.equalsIgnoreCase(System.getenv('MOD_RELEASE')) ? version : "${version}-SNAPSHOT"
            from components.java
        }
    }
}
